// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'config.gradle'
    if (rootProject.hasProperty('extraConfig')) {
        println("loading extra config from: \"${extraConfig}\"...")
        apply from: extraConfig
    }
    repositories(rootProject.ext.config_repos)
    dependencies {
        classpath rootProject.ext.dependencies['gradle']
        classpath rootProject.ext.dependencies['butterknife_gradle']
    }
}

allprojects {
    repositories(rootProject.ext.config_repos)
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            def dep = rootProject.ext.dep_items.find {d -> d.value.group == details.requested.group && d.value.name == details.requested.name}
            if (dep) {
                details.useVersion dep.value.version
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
